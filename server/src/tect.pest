// Horizontal whitespace only
WHITESPACE = _{ " " | "\t" }
ln         = _{ "\r\n" | "\n" }

// Comments and Docs
comment  = @{ "#" ~ (!ln ~ ANY)* }
doc_line =  { comment ~ ln }

// Hard Casing Rules
type_ident = @{ ASCII_ALPHA_UPPER ~ (ASCII_ALPHANUMERIC | "_")* }
var_ident  = @{ ASCII_ALPHA_LOWER ~ (ASCII_ALPHANUMERIC | "_")* }
number     = @{ ASCII_DIGIT+ }
range      =  { number ~ ".." ~ number }
wildcard   =  { "_" }

// Keywords
kw_data   = { "data" }
kw_error  = { "error" }
kw_func   = { "function" }
kw_for    = { "for" }
kw_in     = { "in" }
kw_match  = { "match" }
kw_break  = { "break" }
kw_group  = { "group" }
arrow     = { "->" }
fat_arrow = { "=>" }

// Group Tag (Atomic for better highlighting/hover)
group_tag = @{ "@" ~ ASCII_ALPHA_LOWER ~ (ASCII_ALPHANUMERIC | "_")* }

// Definitions
data_def   = { doc_line* ~ kw_data ~ type_ident }
error_def  = { doc_line* ~ kw_error ~ type_ident }
func_def   = { doc_line* ~ kw_func ~ type_ident ~ "(" ~ type_ident ~ ")" ~ arrow ~ type_union }
type_union = { type_ident ~ ("|" ~ type_ident)* }

// Flow and Logic
instantiation = { doc_line* ~ var_ident ~ ":" ~ type_ident ~ group_tag? }
assignment    = { doc_line* ~ var_ident ~ "=" ~ type_ident ~ "(" ~ var_ident ~ ")" ~ group_tag? }
call          = { doc_line* ~ type_ident ~ "(" ~ var_ident ~ ")" ~ group_tag? }
break_stmt    = { kw_break ~ group_tag? }
for_stmt      = { kw_for ~ var_ident ~ kw_in ~ range ~ "{" ~ (statement | ln)* ~ "}" }
match_stmt    = { kw_match ~ var_ident ~ "{" ~ (match_arm | ln)* ~ "}" }
match_arm     = { (type_ident | wildcard) ~ fat_arrow ~ "{" ~ (statement | ln)* ~ "}" }
group_block   = { kw_group ~ var_ident ~ "{" ~ (statement | ln)* ~ "}" }

statement = _{
    group_block
  | data_def
  | error_def
  | func_def
  | for_stmt
  | match_stmt
  | assignment
  | instantiation
  | call
  | break_stmt
  | comment
  | ln
}

program = { SOI ~ (statement)* ~ EOI }