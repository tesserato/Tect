# ============================================================
# Blog Architecture Specification (.tect)
# ============================================================

# ------------------------------------------------------------
# Type Definitions
# ------------------------------------------------------------

# **Settings**
# The global configuration object.
constant Settings

# **Templates**
# The registry of HTML templates used for rendering.
constant Templates

# **Source File**
# A raw input file from the source directory.
constant SourceFile

# **Article**
# The processed data structure containing content and metadata.
constant Article

# **Initial Command**
# Raw input from the CLI.
variable InitialCommand

# **Path To Config**
# The resolved location of the config file.
variable PathToConfig

# **HTML Article**
# The final article HTML string.
variable HTMLarticle

# **HTML Index**
# The final index HTML string.
variable HTMLIndex

# **Success Report**
# A final summary of operations.
variable SuccessReport

# **File System Error**
# Triggered when IO operations fail.
error FileSystemError

# Group definitions

# **Environment Group**
# Setup and configuration phase.
group Environment

# **Ingestion Group**
# Reading data from the physical world.
group Ingestion

# **Rendering Group**
# Generating the presentation layer.
group Rendering

# **IO Group**
# Committing data back to disk.
group IO

# ------------------------------------------------------------
# Function Definitions
# ------------------------------------------------------------

# **Process CLI**
Environment function ProcessCLI(InitialCommand)
    > Settings
    |  PathToConfig

# **Load Config**
Environment function LoadConfig(PathToConfig)
    > Settings

# **Load Templates**
# This function is utility-focused and remains ungrouped.
function LoadTemplates(Settings)
    > Templates

# **Scan File System**
Ingestion function ScanFS(Settings)
    > [SourceFile]
    |  [FileSystemError]

# **Parse Markdown**
# Core logic often stands alone without a specific group.
function ParseMarkdown(SourceFile)
    > Article
    |  FileSystemError

# **Render HTML Index**
Rendering function RenderHTMLIndex([Article], Settings)
    > HTMLIndex

# **Render HTML Articles**
Rendering function RenderHTMLArticles(Article, Templates, Settings)
    > HTMLarticle

# **Write Index To Disk**
IO function WriteIndexToDisk(HTMLIndex)
    > SuccessReport
    |  FileSystemError

# **Write Articles To Disk**
IO function WriteArticlesToDisk(HTMLarticle)
    > SuccessReport
    |  FileSystemError

# ------------------------------------------------------------
# The Flow (Execution Pipeline)
# ------------------------------------------------------------

ProcessCLI
LoadConfig
LoadTemplates
ScanFS
ParseMarkdown
RenderHTMLArticles
RenderHTMLIndex
WriteArticlesToDisk
WriteIndexToDisk